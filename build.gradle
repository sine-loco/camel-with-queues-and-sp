import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {url 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}" )
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'project-report'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'ru.snm.misc.camel'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
    dependencyManagement {
        imports {
            mavenBom( SpringBootPlugin.BOM_COORDINATES ) {
                bomProperty 'spring.version', '5.1.0.RELEASE'
            }
        }

        dependencies {
            dependencySet( group: 'org.apache.camel', version: '2.22.1' ) {
                entry 'camel-spring-boot-starter'
                entry 'camel-test-spring'
                entry 'camel-jms'
            }


            dependencySet( group: 'org.apache.logging.log4j', version: '2.11.1' ) {
                entry 'log4j-core'
                entry 'log4j-api'
            }

        }
    }

    tasks.withType( JavaCompile ) {
        options.encoding = 'UTF-8'
        options.compilerArgs += '-proc:none'
    }
}

dependencies {
    implementation(
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.boot:spring-boot-starter-activemq',

            'org.apache.camel:camel-spring-boot-starter',
            'org.apache.camel:camel-jms',

            'org.apache.logging.log4j:log4j-core',
            'org.apache.logging.log4j:log4j-api',

            // ---------- JAXB
            /* 'com.sun.xml.bind:jaxb-core' and 'com.sun.xml.bind:jaxb-impl'
             * are already onboard */
            // to avoid 'java.lang.ClassNotFoundException: javax.xml.bind.JAXBContext'
            'javax.xml.bind:jaxb-api:2.3.0',
            // to avoid 'java.lang.ClassNotFoundException: javax.activation.DataHandler'
            // 'javax.activation:activation:1.1.1', there's a newer one:
            'com.sun.activation:javax.activation:1.2.0'
    )

    runtimeOnly( 'org.postgresql:postgresql' )

    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            'ru.yandex.qatools.embed:postgresql-embedded:2.9',
            'org.apache.camel:camel-test-spring',
    )
}



wrapper {
    gradleVersion = '4.10.2'
}